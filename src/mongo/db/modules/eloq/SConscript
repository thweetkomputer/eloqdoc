Import("env")
Import("get_option")

import os
import sys
link_model = get_option('link-model')
SYSTEM_INCLUDE_PATH = [
    '/usr/include',
    '/usr/local/include',
    '/opt/include',
    '/usr/include/x86_64-linux-gnu',
]
def FindIncludePath(file_name):
    """Search for a file in a list of directories."""
    for path in SYSTEM_INCLUDE_PATH:
        candidate = os.path.join(path, file_name)
        if os.path.exists(candidate):
            if os.path.isdir(candidate):
                return candidate
            else:
                return os.path.dirname(candidate)
    return None

def FindLibPath(lib_name):
    """Search for a static or dynamic library in a list of directories."""
    prefixes = ['lib']
    static_ext = '.a'

    if sys.platform == "darwin":
        dynamic_ext = '.dylib'
    else: # Assuming Linux or other Unix-like
        dynamic_ext = '.so'

    # Prefer dynamic link
    extensions = [dynamic_ext, static_ext]
    for ext in extensions:
        for path in env["LIBPATH"]:
            for prefix in prefixes:
                base_name = prefix + lib_name + ext
                candidate = os.path.join(path, base_name)
                print("candidate:",candidate)
                if os.path.exists(candidate):
                    return candidate
    return None


env = env.Clone()
env.InjectMongoIncludePaths()

env["ELOQ_ENGINE_ROOT"]="#src/mongo/db/modules/eloq"


DATA_STORE_TYPE_DYNAMODB          = 1
DATA_STORE_TYPE_BIGTABLE          = 2
DATA_STORE_TYPE_ROCKSDB_CLOUD_S3  = 3
DATA_STORE_TYPE_ROCKSDB_CLOUD_GCS = 4
DATA_STORE_TYPE_ROCKSDB           = 5
DATA_STORE_TYPE_ELOQSTORE         = 6

ROCKSDB_CLOUD_FS_TYPE_S3  = 1
ROCKSDB_CLOUD_FS_TYPE_GCS = 2

LOG_STATE_TYPE_MEM = 1
LOG_STATE_TYPE_RKDB = 2
LOG_STATE_TYPE_RKDB_S3 = 3
LOG_STATE_TYPE_RKDB_GCS = 4

# Read from environment variable (default to ROCKSDB_CLOUD if not set)
data_store_type_str = os.environ.get("WITH_DATA_STORE", "ELOQDSS_ROCKSDB_CLOUD_S3").upper()

# Map the environment variable value to the corresponding constant
if data_store_type_str == "DYNAMODB":
    data_store_type = DATA_STORE_TYPE_DYNAMODB
elif data_store_type_str == "BIGTABLE":
    data_store_type = DATA_STORE_TYPE_BIGTABLE
elif data_store_type_str == "ELOQDSS_ROCKSDB_CLOUD_S3":
    data_store_type = DATA_STORE_TYPE_ROCKSDB_CLOUD_S3
elif data_store_type_str == "ELOQDSS_ROCKSDB_CLOUD_GCS":
    data_store_type = DATA_STORE_TYPE_ROCKSDB_CLOUD_GCS
elif data_store_type_str == "ELOQDSS_ROCKSDB":
    data_store_type = DATA_STORE_TYPE_ROCKSDB
elif data_store_type_str == "ELOQDSS_ELOQSTORE":
    data_store_type = DATA_STORE_TYPE_ELOQSTORE
else:
    # Default fallback (or raise an error if invalid)
    print("Warning: Unknown DATA_STORE_TYPE '{}'. Defaulting to ROCKSDB_CLOUD.".format(data_store_type_str))
    data_store_type = DATA_STORE_TYPE_ROCKSDB_CLOUD_S3

if data_store_type == DATA_STORE_TYPE_ROCKSDB_CLOUD_GCS:
    env.Append(CPPDEFINES=[
        "DATA_STORE_TYPE_ELOQDSS_ROCKSDB_CLOUD_GCS",
    ])
elif data_store_type == DATA_STORE_TYPE_ROCKSDB_CLOUD_S3:
    env.Append(CPPDEFINES=[
        "DATA_STORE_TYPE_ELOQDSS_ROCKSDB_CLOUD_S3",
    ])
elif data_store_type == DATA_STORE_TYPE_ROCKSDB:
    env.Append(CPPDEFINES=[
        "DATA_STORE_TYPE_ELOQDSS_ROCKSDB"
    ])
elif data_store_type == DATA_STORE_TYPE_ELOQSTORE:
    env.Append(CPPDEFINES=[
        "DATA_STORE_TYPE_ELOQDSS_ELOQSTORE"
    ])

if os.environ.get("OPEN_LOG_SERVICE", "1") in ("1", "ON", "on", "true", "True"):
    env.Append(CPPDEFINES=["OPEN_LOG_SERVICE"])
    env["CPPPATH"].extend(["$ELOQ_ENGINE_ROOT/log_service/include"])
else:
    env["CPPPATH"].extend(["$ELOQ_ENGINE_ROOT/eloq_log_service/include"])

with_log_state_str = os.environ.get("WITH_LOG_STATE", "ROCKSDB").upper()
if with_log_state_str == "MEMORY":
    with_log_state = LOG_STATE_TYPE_RKDB_MEMORY
    env.Append(CPPDEFINES=["LOG_STATE_TYPE_MEM"])
elif with_log_state_str == "ROCKSDB":
    with_log_state = LOG_STATE_TYPE_RKDB
    env.Append(CPPDEFINES=["LOG_STATE_TYPE_RKDB"])
elif with_log_state_str == "ROCKSDB_CLOUD_S3":
    with_log_state = LOG_STATE_TYPE_RKDB_S3
    env.Append(CPPDEFINES=["LOG_STATE_TYPE_RKDB_S3"])
elif with_log_state_str == "ROCKSDB_CLOUD_GCS":
    with_log_state = LOG_STATE_TYPE_RKDB_GCS
    env.Append(CPPDEFINES=["LOG_STATE_TYPE_RKDB_GCS"])
else:
    # Default fallback (or raise an error if invalid)
    print("Error: Unknown WITH_LOG_STATE '{}'.".format(with_log_state))
    Exit(1)

if os.environ.get("FORK_HM_PROCESS", "0") in ("1", "ON", "on", "true", "True"):
    env.Append(CPPDEFINES=["FORK_HM_PROCESS"])

env.Append(CPPDEFINES=[
    "OVERRIDE_GFLAGS_NAMESPACE",
    "BRPC_WITH_GLOG",
    # "BRPC_ENABLE_CPU_PROFILER",
    "EXT_TX_PROC_ENABLED",
    "STATISTICS",
    ("GFLAGS_NAMESPACE", "gflags")
])
env["CPPPATH"].extend(SYSTEM_INCLUDE_PATH)
env["CPPPATH"].extend(
    [
        "$ELOQ_ENGINE_ROOT",
        "$ELOQ_ENGINE_ROOT/src",
        "$ELOQ_ENGINE_ROOT/src/base",
        "$ELOQ_ENGINE_ROOT/src/eloq_data_store_service",
        "$ELOQ_ENGINE_ROOT/tx_service/include",
        "$ELOQ_ENGINE_ROOT/tx_service/include/cc",
        "$ELOQ_ENGINE_ROOT/tx_service/include/fault",
        "$ELOQ_ENGINE_ROOT/tx_service/include/proto",
        "$ELOQ_ENGINE_ROOT/tx_service/include/remote",
        "$ELOQ_ENGINE_ROOT/tx_service/include/store",
        "$ELOQ_ENGINE_ROOT/tx_service/include/sequences",
        "$ELOQ_ENGINE_ROOT/tx_service/tx-log-protos",
        "$ELOQ_ENGINE_ROOT/tx_service/abseil-cpp",
        "$ELOQ_ENGINE_ROOT/store_handler",
        "$ELOQ_ENGINE_ROOT/eloq_metrics/include",
        FindIncludePath("mimalloc-2.1"),
        FindIncludePath("aws/core"),
        FindIncludePath("aws/kinesis"),
        FindIncludePath("aws/s3"),
        FindIncludePath("google/cloud/storage"),
        FindIncludePath("rocksdb_cloud_header/"),
        FindIncludePath("rocksdb_cloud_header/rocksdb"),
        FindIncludePath("rocksdb_cloud_header/rocksdb/cloud"),
        # FindIncludePath("gperftools/heap-profiler.h")
    ]
)

if data_store_type in [DATA_STORE_TYPE_ROCKSDB_CLOUD_S3, DATA_STORE_TYPE_ROCKSDB_CLOUD_GCS]:
   _rkdb_cloud_includes = [
       FindIncludePath("rocksdb_cloud_header/"),
       FindIncludePath("rocksdb_cloud_header/rocksdb"),
       FindIncludePath("rocksdb_cloud_header/rocksdb/cloud"),
   ]
   env["CPPPATH"].extend([p for p in _rkdb_cloud_includes if p])
   if data_store_type == DATA_STORE_TYPE_ROCKSDB_CLOUD_S3:
      _aws_includes = [
          FindIncludePath("aws/core"),
          FindIncludePath("aws/kinesis"),
          FindIncludePath("aws/s3"),
      ]
      env["CPPPATH"].extend([p for p in _aws_includes if p])
   elif data_store_type == DATA_STORE_TYPE_ROCKSDB_CLOUD_GCS:
      _gcs_includes = [FindIncludePath("google/cloud/storage")]
      env["CPPPATH"].extend([p for p in _gcs_includes if p])
elif data_store_type == DATA_STORE_TYPE_ROCKSDB:
    env["CPPPATH"].extend([FindIncludePath("rocksdb")])
elif data_store_type == DATA_STORE_TYPE_ELOQSTORE:
    env["CPPPATH"].extend(["$ELOQ_ENGINE_ROOT/store_handler/eloq_data_store_service/eloqstore"])

base_deps = [
    # FindLibPath("absl_bad_variant_access"),
    # FindLibPath("absl_bad_optional_access"),
    # FindLibPath("absl_str_format_internal"),
    #
    # mimalloc and brpc are also linked to eloqdoc in mongo/SConscript.
    FindLibPath("mimalloc"),
    FindLibPath("brpc"),
    FindLibPath("braft"),
    FindLibPath("gflags"),
    FindLibPath("protobuf"),
    FindLibPath("glog"),
    # FindLibPath("tcmalloc_and_profiler"),
]

if with_log_state == LOG_STATE_TYPE_RKDB_GCS:
    log_deps = [
        FindLibPath("google_cloud_cpp_common"),
        FindLibPath("google_cloud_cpp_storage"),
        FindLibPath("rocksdb-cloud-gcp"),
    ]
elif with_log_state == LOG_STATE_TYPE_RKDB_S3:
    log_deps = [
        FindLibPath("aws-cpp-sdk-core"),
        FindLibPath("aws-cpp-sdk-kinesis"),
        FindLibPath("aws-cpp-sdk-s3"),
        FindLibPath("rocksdb-cloud-aws"),
    ]
elif with_log_state == LOG_STATE_TYPE_RKDB:
    log_deps = [FindLibPath("rocksdb")]
else:
    log_deps = []

if data_store_type == DATA_STORE_TYPE_ROCKSDB_CLOUD_GCS:
    store_deps = [
        FindLibPath("google_cloud_cpp_common"),
        FindLibPath("google_cloud_cpp_storage"),
        FindLibPath("rocksdb-cloud-gcp"),
    ]
elif data_store_type == DATA_STORE_TYPE_ROCKSDB_CLOUD_S3:
    store_deps = [
        FindLibPath("aws-cpp-sdk-core"),
        FindLibPath("aws-cpp-sdk-kinesis"),
        FindLibPath("aws-cpp-sdk-s3"),
        FindLibPath("rocksdb-cloud-aws"),
    ]
elif data_store_type == DATA_STORE_TYPE_ROCKSDB:
   store_deps = [FindLibPath("rocksdb")]
elif data_store_type == DATA_STORE_TYPE_ELOQSTORE:
   store_deps = []

eloq_dependencies = base_deps + store_deps + log_deps

env.Append(LIBPATH=["$ELOQ_ENGINE_ROOT/build/tx_service/abseil-cpp/absl/base"])
env.Append(LIBS=["absl_log_severity", "absl_raw_logging_internal"])

print("CPPDEFINES: ", env["CPPDEFINES"])
print("CPPPATH: ", env["CPPPATH"])
print("LIBPATH: ", env["LIBPATH"])
print("LIBS: ", env["LIBS"])
print("LINKFLAGS: ", env["LINKFLAGS"])
print("eloq_dependencies: ", eloq_dependencies)
print("MALLOC:", env['MONGO_ALLOCATOR'])


env.Append(LIBPATH=["$ELOQ_ENGINE_ROOT/build"])

# Ensure the linker can find CMake-produced shared libs and link them explicitly.
# CMake installs txservice/logservice/datastore/eloq_metrics into DEST_DIR/lib.
dest_dir = os.environ.get("DEST_DIR", "")
dest_lib_dir = os.path.join(dest_dir, "lib") if dest_dir else None
if dest_lib_dir and os.path.isdir(dest_lib_dir):
    env.Append(LIBPATH=[dest_lib_dir])

# Always link against the CMake-built module libraries to resolve txlog and service symbols.
cmake_module_libs = ["logservice", "txservice", "datastore", "eloq_metrics"]
env.Append(LIBS=cmake_module_libs)

# For RocksDB Cloud backends, ensure AWS/GCS SDKs are linked when needed because
# this module directly references AWS Init/Shutdown APIs under S3 builds.
if data_store_type == DATA_STORE_TYPE_ROCKSDB_CLOUD_S3:
    env.Append(LIBS=["aws-cpp-sdk-core"])  # Aws::InitAPI/Aws::ShutdownAPI
    env.Append(LIBS=["aws-cpp-sdk-kinesis", "aws-cpp-sdk-s3", "rocksdb-cloud-aws"])
elif data_store_type == DATA_STORE_TYPE_ROCKSDB_CLOUD_GCS:
    env.Append(LIBS=["google_cloud_cpp_common"])
    env.Append(LIBS=["google_cloud_cpp_storage", "rocksdb-cloud-gcp"])

if with_log_state == LOG_STATE_TYPE_RKDB_S3:
    env.Append(LIBS=["aws-cpp-sdk-core"])  # Aws::InitAPI/Aws::ShutdownAPI
    env.Append(LIBS=["aws-cpp-sdk-kinesis", "aws-cpp-sdk-s3", "rocksdb-cloud-aws"])
elif with_log_state == LOG_STATE_TYPE_RKDB_GCS:
    env.Append(LIBS=["google_cloud_cpp_common"])
    env.Append(LIBS=["google_cloud_cpp_storage", "rocksdb-cloud-gcp"])
    env.Append(LIBS=[
        "google_cloud_cpp_common",
        "google_cloud_cpp_storage",
        "rocksdb-cloud-gcp",
        "rocksdb",
        "absl_base",
        "absl_strings",
        "absl_str_format_internal",
        "absl_time",
        "absl_time_zone",
        "absl_civil_time",
        "absl_int128",
        "absl_spinlock_wait",
        "absl_throw_delegate",
        "absl_raw_logging_internal",
        "absl_malloc_internal",
        "absl_log_severity",
        "crypto",
        "ssl",
        ])

env.Library(
    target="storage_eloq_core",
    source=[
        "src/eloq_kv_engine.cpp",
        "src/eloq_record_store.cpp",
        "src/eloq_recovery_unit.cpp",
        "src/eloq_index.cpp",
        "src/eloq_cursor.cpp",
        "src/eloq_options_init.cpp",
        "src/eloq_global_options.cpp",
        "src/base/eloq_key.cpp",
        "src/base/eloq_record.cpp",
        "src/base/eloq_table_schema.cpp",
        "src/base/eloq_catalog_factory.cpp",
        "src/base/eloq_util.cpp",
        "src/base/metrics_registry_impl.cpp",
    ],
    LIBDEPS=[
        "$BUILD_DIR/mongo/base",
        "$BUILD_DIR/mongo/db/namespace_string",
        "$BUILD_DIR/mongo/db/server_options_servers",
        "$BUILD_DIR/mongo/db/storage/key_string",
        "$BUILD_DIR/mongo/db/storage/kv/kv_prefix",
        "$BUILD_DIR/mongo/db/service_context",
        "$BUILD_DIR/mongo/db/storage/index_entry_comparison",
        "$BUILD_DIR/mongo/db/storage/oplog_hack",
        "$BUILD_DIR/mongo/db/storage/kv/kv_engine",
        "$ELOQ_ENGINE_ROOT/build/txservice",
        "$ELOQ_ENGINE_ROOT/build/logservice",
        "$ELOQ_ENGINE_ROOT/build/datastore",
        "$ELOQ_ENGINE_ROOT/build/eloq_metrics",
    ],
    LIBDEPS_PRIVATE=[],
    SYSLIBDEPS=eloq_dependencies,
)

env.Library(
    target="storage_eloq",
    source=[
        "src/eloq_init.cpp",
    ],
    LIBDEPS=[
        # "$BUILD_DIR/mongo/db/db_raii",
        # "$BUILD_DIR/mongo/db/storage/storage_engine_impl",
        "$BUILD_DIR/mongo/db/storage/storage_engine_lock_file",
        "$BUILD_DIR/mongo/db/storage/storage_engine_metadata",
        "$BUILD_DIR/mongo/db/storage/kv/kv_engine",
        "storage_eloq_core",
    ],
    LIBDEPS_PRIVATE=[
        "$BUILD_DIR/mongo/db/storage/storage_engine_common",
    ],
    LIBDEPS_DEPENDENTS=["$BUILD_DIR/mongo/db/serveronly"],
)
